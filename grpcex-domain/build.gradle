apply plugin: 'com.google.protobuf'

dependencies {
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation "io.grpc:grpc-protobuf:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"
    implementation "com.salesforce.servicelibs:reactor-grpc:$reactive_grpc_version"
    implementation "com.salesforce.servicelibs:reactor-grpc-stub:$reactive_grpc_version"
    implementation "io.projectreactor:reactor-core:$reactor_version"
}

sourceSets {
    main {
        java {
            srcDir 'src/main/protoGen'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.6.1'
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.21.0"
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:${reactive_grpc_version}:jdk8@jar"
        }
    }

    generateProtoTasks {
        ofSourceSet('main').each { task ->
            task.builtins {
                java{
                    outputSubDir = 'protoGen'
                }
            }
            task.plugins {
                grpc {
                    outputSubDir = 'protoGen'
                }
                reactor {
                    outputSubDir = 'protoGen'
                }

            }
        }
    }

    generatedFilesBaseDir = "$projectDir/src/"
}

task cleanProtoGen{
    doFirst{
        delete("$projectDir/src/main/protoGen")
    }
}

clean.dependsOn cleanProtoGen
